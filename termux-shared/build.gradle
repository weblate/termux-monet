apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "com.termux.shared"
    compileSdkVersion project.properties.compileSdkVersion.toInteger()

    dependencies {
        implementation "androidx.appcompat:appcompat:1.7.0"
        implementation "androidx.annotation:annotation:1.8.0"

        implementation "androidx.core:core:1.13.1"
        implementation "com.google.android.material:material:1.12.0"
        implementation "com.google.guava:guava:32.0.1-jre"
        implementation "io.noties.markwon:core:$markwonVersion"
        implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
        implementation "io.noties.markwon:linkify:$markwonVersion"
        implementation "io.noties.markwon:recycler:$markwonVersion"
        implementation "com.github.ChickenHook:RestrictionBypass:2.2"

        implementation "androidx.window:window:1.3.0"

        // Bumping commons-io to a version higher than 2.5
        // will cause runtime exceptions on android < 8 due
        // to missing classes like java.nio.file.Path.
        implementation "commons-io:commons-io:2.16.1"

        implementation project(":terminal-view")

        implementation project(":termux-am-library")
    }

    defaultConfig {
        compileSdkVersion project.properties.compileSdkVersion.toInteger()
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndkBuild {
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }
}

dependencies {
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'com.termux'
                artifactId = 'termux-shared'
                version = '0.118.0'
                artifact(sourceJar)
            }
        }
    }
}
